// SPDX-FileCopyrightText: TNG Technology Consulting GmbH
//
// SPDX-License-Identifier: Apache-2.0

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'checkstyle'
}

group 'com.tngtech.spdx'
version '1.0-SNAPSHOT'

subprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'checkstyle'
    
    group = rootProject.group

    checkstyle {
        toolVersion = '9.3'
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        configProperties = [
                'checkstyle.cache.file': "${buildDir}/checkstyle.cache",
        ]
        ignoreFailures = false
        showViolations = true
        maxWarnings = 0
    }

    dependencies {
        implementation('org.spdx:tools-java:1.1.0')

        constraints {
            implementation("org.spdx:java-spdx-library") {
                version {
                    require "1.1.1"
                }
                because 'Important bugfixes'
            }
            implementation("org.spdx:spdx-jackson-store") {
                version {
                    require "1.1.1"
                }
                because 'Important bugfixes'
            }
        }
    }
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                'Main-Class': 'org.spdx.testbed.Main'
        )
    }
}
